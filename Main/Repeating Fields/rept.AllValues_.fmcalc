// rept.AllValues_ ( _repeating_field; _repetition_max; _repetition )
//
// Purpose:		Returns all of the values of the repeating field as a list.
//
// Parameters:		_repeating_field:	Pass the field itself (as if passing the field value), not using
//								GetFieldName, because the custom function engine will interpret
//								the parameter as a reference.
//				_repetition_max:	How many repetitions the field contains
//				_repetition:		The current repetition
//
// Author:			Will M. Baker
// Version:		1.0 written 11-10-20
//
// Notes:			http://buzz.beezwax.net/2011/10/29/working-with-repeating-fields
//
// Example:
// // Assuming a repeating field with 5 repetitions with "apple", "orange", "", "pear", ""...
// rept.AllValues_ ( repeating_field; 5; 1 ) = "apple¶orange¶¶pear¶¶"

Case (
  _repetition > _repetition_max;
  devp.Nil;

  Let (
    [
      _value		= GetRepetition ( _repeating_field; _repetition );
      _rest		= rept.AllValues_ ( _repeating_field; _repetition_max; _repetition + 1 );

      _start = Case (
        _repetition = 1;		devp.Nil;
					"¶"
      );

      _result = _start & _value & _rest
    ];

    _result
  )
)
