// lsts.ValueFilter ( _match; _list )
//
// Purpose:		Extract a return delimited list of values based on the supplied match value.
// Parameters:		_match:	The target match value
//              		_list:		The list of values
//
// Requirements:	lsts.CustomList
//				devp.Null
//
// Author:			Matt Petrowsky
// Version:		1.1 written 11-07-08
//				1.2 edited 15-03-14 by Charles Ross, clear out local variables before existing
//
// Notes:			This is a simple straight match function. It does not find values based on Regular
//				Expressions.
//
// Example:
// lsts.ValueFilter ( "teen"; List ( "twelve"; "thirteen"; "fourteen" ) ) = List ( "thirteen"; "fourteen" )

Let(
  [
    $_ValueFilter_value_list	= _list;
    $_ValueFilter_match	= _match;

    _function = "
      Let (
        _value = GetValue( $_ValueFilter_value_list ; [n] );

        Case (
          PatternCount ( _value ; $_ValueFilter_match ) ;
          _value
        )
      )
    ";

    _result = lsts.CustomList ( 1; ValueCount ( $_ValueFilter_value_list ); _function );

    $_ValueFilter_value_list	= devp.Nil;
    $_ValueFilter_match	= devp.Nil
  ];

  _result

)
