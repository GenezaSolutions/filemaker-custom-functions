// rand.StreetAddress
//
// Purpose:		Returns a random fake street address.
//
// Requirements:	devp.Nil
//				rand.Integer
//				rand.ListItem
//
// Author:			Mislav Kos <mkos@soliantconsulting.com>
// Version:		1.0 written 14-10-15
//				1.1 edited 15-05-22 by Charles Ross for readability and use of sub functions
//
// Example:
// // One possible return valueâ€¦
// rand.StreetAddress = "85 S. Smith Ct."

Let (
  [ 
    _directions	= Choose (
      rand.Integer ( 0; 1 );	List ( "N. "; "S. "; "E. "; "W. " );
					devp.Nil
    );

    _generic = List ( "St"; "Ave"; "Rd"; "Ln"; "Blvd."; "Ct."; "Wy."; "Dr."  ) ; 

    _specific = List (
      "Cedar";	"Center";		"Cherry";		"Chestnut";	"Church";
      "Elm";		"Forest";		"Hickory";		"Highland";	"Hill";
      "Jackson";	"Jefferson";	"Johnson";		"Lake";		"Lincoln";
      "Main";	"Maple";		"Meadow";		"Mill";		"Oak";
      "Park";		"Peach";		"Peoria";		"Pine";		"Railroad";
      "Ridge";	"River";		"Smith";		"Spring";		"Sunset";
      "Valley";	"Walnut";		"Washington";	"Willow";		"Wilson"
    );

    _number = Choose (
      rand.Integer ( 0; 3 );	rand.Integer ( 1; 99 );
					rand.Integer ( 100; 199 );
					rand.Integer ( 1000; 1999);
					rand.Integer ( 10000; 19999 )
    );

    _result = _number & " " &
      rand.ListItem ( _directions ) &
      rand.ListItem ( _specific ) & " " &
      rand.ListItem ( _generic )
  ] ;

  _result
)
