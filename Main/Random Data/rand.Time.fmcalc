// rand.Time ( _min; _max )
//
// Purpose:		Returns a randome time between the low and high parameters.
//
// Parameters:		_min:	The low end of the time range, if blank, uses 0:00:00
//				_max:	The high end of the time range, if blank, uses 23:59:59
//
// Requirements:	rand.Integer
//				devp.Nil
//
// Author:			Mislav Kos <mkos@soliantconsulting.com>
// Version:		1.0 written 14-10-15
//				1.1 edited 15-05-22 by Charles Ross for readability and use of sub functions
//
// Notes:			If invalid value are specified for the low and/or high parameters, or if the 'high' value is
//				lower (earlier) than the 'low' value, then the result will be blank
//
// Example:
// // One possible return value…
// rand.Time ( GetAsTime ( "1:23" ); GetAsTime ( "6:43" ) ) = "6:26:56"

Let (
  [
    _min = Case (
      GetAsTime ( _min ) = "?" or IsEmpty ( _min );	0;
										_min
    );

    _max = Case (
      GetAsTime ( _max ) = "?" or IsEmpty ( _max );	( 60 * 60 * 24 ) - 1;
										_max
    );

    _time = GetAsTime ( rand.Integer ( _min; _max ) );

    _result = Case (
      _max ≥ _min ;	_time;
				devp.Nil
    )
  ] ; 

  _result
)
