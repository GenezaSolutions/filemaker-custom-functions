// rand.Date
//
// Purpose:		Returns a random date between the passed parameters or between the beginning of a
//				low year and the end of a high year.
//
// Parameters:		_low:		the low end of the date range; you can specify just the year instead of a full
//						date
//				_high:	the high end of the date range; you can specify just the year instead of a full
//						date
//
// Requirements:	nump.RandomInteger
//
// Author:			Mislav Kos <mkos@soliantconsulting.com>
// Version:		1.0 written 14-10-15
//				1.1 edited 15-05-22 by Charles Ross for readability and use of sub functions
//
// Example:

Let (
  [
    _valid_date = IsValid ( GetAsDate ( GetAsText ( _low ) ) ) ; 
    _valid_year = _low > 0 and _low ≤ 4000 ; 

    _start = Case (
      _valid_date ;	GetAsDate ( _low ) ;
	_valid_year ;	Date ( 1 ; 1 ; _low ) // _start will be empty if low is invalid
    );
	
    _valid_date = IsValid ( GetAsDate ( GetAsText ( _high ) ) ) ; 
    _valid_year = _high > 0 and _high ≤ 4000 ; 

    _end = Case (
      _valid_date ;	GetAsDate ( _high ) ;
      _valid_year ;	Date ( 12 ; 31 ; _high ) // _end will be empty if high is invalid
    );
	
    _days = _end - _start + 1 ; 
    _date = GetAsDate ( _start + rand.Integer ( 0; _days ) - 1 );

    _result = Case (
      not IsEmpty ( _start ) and not IsEmpty ( _end ) and _end ≥ _start ;
      _date
    )
  ] ; 

  _result
)
