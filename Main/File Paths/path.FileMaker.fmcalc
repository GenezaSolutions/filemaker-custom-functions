// path.FMPath ( _os_path )
//
// Purpose:		Converts an operating system specific path to a FileMaker format.
//
// Parameters:		_os_path:	The operating system specific path to convert.
//
// Requirements: 	The BaseElements plugin
//				txtp.TrimStart
//				path.ProtocolPlatform
//				sysk.PlatformIsMac
//				sysk.PlatformIsWin
//
// Author:			Nicholas Orr
// Version:		1.0 written 11-06-03
//
// Notes:			http://www.goya.com.au/blog/guide-filemaker-import-paths
//
// Example:
// sample = result

Let (
  [ 
    _users			= "/Users" ; 
    _users_len		= Length ( _users ) ; 
    _username		= txtp.TrimStart ( _os_path ; _users_len + 1 ) ; 
    _username		= Left ( _username ; Position ( _username ; "/" ; 1 ; 1 ) - 1 ) ; 
    _system_drive	= Get ( SystemDrive ) ; 
    _system_drive	= Left ( _system_drive ; Length ( _system_drive ) - 1 ) ; 
    _volume		= "/Volumes" ; 
    _volume_len		= Length ( _volume ) ; 
    _path_len		= Length ( _os_path ) ; 

    _is_filevault = sysk.PlatformIsMac
      and ( Left ( _os_path ; _users_len ) = _users )
      and BE_FileExists ( _users &"/." & _username ) ; 

    _is_external_drive = ( Left ( _os_path ; _volume_len ) = _volume ) ;

    _external_path	= Right ( _os_path ; _path_len - _volume_len ) ; 
    _filevault_path	= Right ( _os_path ; _path_len - _users_len ) ; 

    _mac_path = Case (
      _is_external_drive ;	_external_path ;
      _is_filevault ;		_filevault_path ;
					_system_drive & _os_path
    ) ; 

    _win_path = Case ( Left ( _os_path ; 2 ) â‰  "\\\\" ; "/" ) & Substitute ( _os_path ; "\\" ; "/" );

    _result = path.ProtocolPlatform & Case (
	sysk.PlatformIsMac ;	_mac_path ;
        sysk.PlatformIsWin ;	_win_path
    )
  ] ; 

  _result
)
