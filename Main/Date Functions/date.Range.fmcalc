// date.Range ( _date_1; _date_2 )
//
// Purpose:		Return a list of dates between the first and last date, inclusive.
//
// Parameters:		_date_1: The earlier date (will be corrected if not earlier)
//				_date_2: The later date (will be corrected if not later)
//
// Author:			Charles Ross
// Version:		1.0 written 15-03-06
//
// Example:
// date.Range ( Date ( 3; 31; 2015 ); Date ( 4; 2; 2015 ) ) = "3/31/2015¶4/1/2015¶4/2/2015"

Let (
  [
    _date_1 = GetAsDate ( _date_1 );
    _date_2 = GetAsDate ( _date_2 );

    _earlier	= Min ( _date_1; _date_2 );
    _later	= Max ( _date_1; _date_2 )
  ];

  GetAsText ( _earlier ) &
  Case (
    _earlier = _later;	devp.Nil;
				"¶" & date.Range ( _earlier + 1; _later )
  )
)
