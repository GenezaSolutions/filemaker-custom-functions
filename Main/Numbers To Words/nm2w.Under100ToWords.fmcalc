// nm2w.Under100ToWords ( _num )
//
// Purpose:		Convert a small integer (<99) to english words.
//
// Parameters:		_num:	The number to convert
//
// Author:			The Shadow
// Version:		1.0 written 15-03-11
//
// Example:
// nm2w.Under100ToWords ( 87 ) = "eighty-seven"

Let(
  [
    _tenth	= _num / 10;
    _tens	= Int( _tenth ) * 10;

    _under_twenty = Choose(
      _num;
      "zero"; "one"; "two"; "three"; "four"; "five"; "six"; "seven"; "eight"; "nine"; "ten"; "eleven"; "twelve";
        "thirteen"; "fourteen"; "fifteen"; "sixteen"; "seventeen"; "eighteen"; "nineteen"
    );

    _over_twenty = Choose(
      Int( _tenth ) - 2;
      "twenty"; "thirty"; "fourty"; "fifty"; "sixty"; "seventy"; "eighty"; "ninety"
    );

    _ten_multiple 			= ( _tens = _tenth * 10 );
    _non_ten_multiple_portion	= Case (
      not _ten_multiple;	"-" & nm2w.Under100ToWords( _num - _tens );
					devp.Nil
    );

    _over_twenty = _over_twenty & _non_ten_multiple_portion;

    _result =   Case(
      _num â‰¥ 100;	"<error - too large number in ConvertSmallIntToWords: " & _num & ">";
      _num < 20;		_under_twenty;
				_over_twenty
    )
  ];

  _result
)
