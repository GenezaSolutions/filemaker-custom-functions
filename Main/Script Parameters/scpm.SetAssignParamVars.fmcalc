// scpm.SetAssignParamVars
//
// Purpose:		A utility function for scpm.AssignParams that sets up the script variables used.
//
// Requirements: 	scpm.WordsToList
//				devp.Nil
//
// Author:			Charles Ross, inspired by Alexander Zueiv
// Version:		1.0 written 15-03-11
//
// Notes:			See notes for scpm.AssignParams for full discussion.
//
// Example:
// // No return value, so...
// ( scpm.SetAssignParamVars & ( $_scpm_open_char = "(" ) ) = 1

Let (
  [
  $_scpm_script			= Get( ScriptName );
  $_scpm_raw_params	= Get( ScriptParameter );
  $_scpm_params		= Case(
    Right( $_scpm_raw_params; 2 ) = "; ";	Left( $_scpm_raw_params; Length( $_scpm_raw_params ) - 2 );
								$_scpm_raw_params
  );

  $_scpm_open_char			= "(" ;    // beginning of parameters definition
  $_scpm_close_char			= ")";     // end of parameters definition
  $_scpm_break_char			= ";" ;    // regular parameters separator (and)
  $_scpm_alt_open_char		= "[";     // beginning of alternative parameters definition.
  $_scpm_alt_close_char		= "]";     // end of alternative parameters definition.
  $_scpm_alt_char			= "|" ;    // alternative parameters separator (or)
  $_scpm_opt_char			= "{" ;    // beginning of optional parameters section

  _start_pos		= Position( $_scpm_script; $_scpm_open_char; 1; 1 ) + 1;
  _param_len		= Position( $_scpm_script; $_scpm_close_char; Length( $_scpm_script ); -1 )
				  - Position( $_scpm_script; $_scpm_open_char; 1; 1 ) - 1;

  $_scpm_param_template = Middle( $_scpm_script; _start_pos; _param_len );

  // Convert the parameters to a list that can be interated through.
  $_scpm_param_list = scpm.WordsToList( $_scpm_param_template );

  $_scpm_empty_param_template = "( not IsEmpty( $ ) )"
  ];

  devp.Nil
)
