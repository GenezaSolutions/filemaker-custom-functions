// Returns the drive name from the path with no reference to Get( SystemDrive ). Therefore a POSIX
//   path will return an empty string if it doesn't reference the /Volumes directory.

Let(
  [
    _type = path.Type( _path );

    // Get a list of the directory structure so we can use GetValue.
    _path_list = path.List( _path );

    _drive_name = Case(
      _type = path.TypeFileMakerWinNetwork;		GetValue( _path_list; 3 );
      path.TypeIsFileMaker( path.Type( _path ) );	GetValue( _path_list; 2 );

      ( _type = path.TypeHFS )
        or ( _type = path.TypeWin )
        or ( _type = path.TypeWinBackslash );	GetValue( _path_list; 1 );

      _type = path.TypePOSIX;					Case(
										  GetValue( _path_list; 2 ) = "Volumes";
										  GetValue( _path_list; 3 );
										  devp.Null
										)
    )
  ];

  _drive_name
)
