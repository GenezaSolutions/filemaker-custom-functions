// devp.DeveloperProperties ( _hide )
//
// Purpose:	Set reserved namespace variables of $$CURRENT.WHATEVER to contain
//			useful developer information for the purpose of viewing within the Data
//			Viewer. Typically used within conditional formatting on layout text label.
// Parameters:	_hide: If True, hides the calculation completely, even from developers.
//
// Requirements: devp.LayouObjectHierarchy, devp.ObjectID, devp.Nil
//
// Author:		Matt Petrowsky
// Version:	1.0
//
// Example:
// // If executed while a developer is logged in...
//  ( devp.DeveloperProperties ( True ) = True )
//    and ( $$CURRENT.TABLE = Get ( LayoutTableName ) )
//    and ( $$CURRENT.LAYOUT = Get ( LayoutName ) )

Case (
  devp.IsDeveloper; // only set global variables for developers to prevent snooping
  Let (
    [
      $$CURRENT.TABLE		= Get ( LayoutTableName );
      $$CURRENT.LAYOUT		= Get ( LayoutName );
      $$CURRENT.FIELD		= Get ( ActiveFieldName );
      $$CURRENT.FIELDTABLE	= Get ( ActiveFieldTableName );
      $$CURRENT.OBJECTS		= devp.LayoutObjectHierarchy ( devp.Nil );
      $$CURRENT.FILEPATH		= Get ( FilePath );

      $$CURRENT.OBJECTIDS = "Layout ID = " &
        devp.ObjectID ( Get ( LayoutName ); "Layout"; devp.Nil; devp.Nil )
        & " | " & "Table ID = " &
        devp.ObjectID ( Get ( LayoutTableName ); "Table"; devp.Nil; devp.Nil )
    ];

    // This result is intended to be used on conditional formatting
    // when the text is set to hide using the same color as the background
    _hide
  );

  True // Always return true for non developer when used on conditional formatting
)
