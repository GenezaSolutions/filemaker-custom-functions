/**
 * =====================================================
 * DeveloperProperties( _hide )
 * 
 * PURPOSE:
 *    Set reserved namespace variables of $$CURRENT.WHATEVER to contain useful
 *    developer information for the purpose of viewing within the Data Viewer.
 *    Typically used within conditional formatting on layout text label.
 * 
 * PARAMETERS:
 *    @hide = (boolean) True or False
 * 
 * RETURNS:
 *    (boolean) True or False
 * 
 * DEPENDENCIES:
 *    Developer(), ObjectID(), LayoutObjectHierarchy()
 * 
 * NOTES:
 *    Note the use of the space after the $$
 *    this causes these global variables to
 *    be sorted at the top of the Data Viewer
 * =====================================================
 * 
 */

Case( devp.IsDeveloper; // only set global variables for developers to prevent snooping
  Let(
    [
      $$CURRENT.TABLE      = Get( LayoutTableName );
      $$CURRENT.LAYOUT     = Get( LayoutName );
      $$CURRENT.FIELD      = Get( ActiveFieldName );
      $$CURRENT.FIELDTABLE = Get( ActiveFieldTableName );
      $$CURRENT.OBJECTS    = devp.LayoutObjectHierarchy( devp.Nil );
      $$CURRENT.FILEPATH   = Get( FilePath );

      $$CURRENT.OBJECTIDS = "Layout ID = " &
        devp.ObjectID( Get( LayoutName ); "Layout"; devp.Nil; devp.Nil )
        & " | " & "Table ID = " &
        devp.ObjectID( Get( LayoutTableName ); "Table"; devp.Nil; devp.Nil )
    ];

    // This result is intended to be used on conditional formatting
    // when the text is set to hide using the same color as the background
    _hide
  );

  True // Always return true for non developer when used on conditional formatting
)
