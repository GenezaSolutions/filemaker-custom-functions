/* FMNID_Table ( _name_id )

if _Name_ID is an ID, returns the name
if _Name_ID is a name, returns the ID

Author: Fabrice Nordmann
Minor Changes: Kevin Frank

Original documentation & explanation:
http://www.fmfunctions.com/fname/FM_Name_ID
http://www.1-more-thing.com/FileMaker-Avoid-Hard-coding.html

*/

Let ([

    _fileName = Get ( FileName ) ; 
    _p = _name_id ;
    _names = TableNames ( _fileName ) ; 
    _ids = TableIDs ( _fileName ) 
];

Case ( _p = GetAsNumber ( _p ) ;

    // ID -> Name
    Case ( not IsEmpty ( FilterValues ( _p ; _ids )) ;
        GetValue ( _names ; Let ([ _text = _ids ; _item = _p ; _adj = ¶ & _text & ¶ ] ; PatternCount ( Left ( _adj ; Position ( _adj ; ¶ & _item & ¶ ; 1 ; 1 ) + 1 ) ; ¶ )))
    ) ;

// Name -> ID
    Case ( not IsEmpty ( FilterValues ( _p ; _names )) ;
        GetValue ( _ids ; Let ([ _text = _names ; _item = _p ; _adj = ¶ & _text & ¶ ] ; PatternCount ( Left ( _adj ; Position ( _adj ; ¶ & _item & ¶ ; 1 ; 1 ) + 1 ) ; ¶ )))
    )

)
)
