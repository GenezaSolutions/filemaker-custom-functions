/**
 * =====================================================
 * IsNumber ( value )
 * 
 * PURPOSE:
 *    Determine if the supplied value is numeric
 * 
 * PARAMETERS:
 *    value = (string or number)
 * 
 * RETURNS:
 *    (boolean) whether value is numeric or not
 * 
 * DEPENDENCIES:
 *    none
 * 
 * NOTES:
 *    Adapted from the following function
 *    http://fmfunctions.com/functions_display_record.php?functionId=161
 * =====================================================
 * 
 */

Let(
  [
    _decimalSeparator = Middle( 3 / 2; 2; 1 ); // do some division to get separator
    _cleanedValue = Substitute(
      _value ;
      [ "e+" ; "" ];
      [ "E+" ; "" ];
      [ "e-" ; "" ];
      [ "E-" ; "" ]
    );
    _filteredValue = Filter( _cleanedValue; "1234567890-" & _decimalSeparator );
    _decimalCount = PatternCount( _value; _decimalSeparator )
  ];

  Exact( _filteredValue ; _cleanedValue )
    and ( Left( _value; 1 ) ≠ "0"
      or _value = "0"
      or Left( _value; 2 ) = "-0"
      or Left( _value; 2 ) = "0" & _decimalSeparator
    )
    and _decimalCount ≤ 1
    and _value ≠ "-"
)

/*
// Unit tests
List(
  "These should be true";
  IsNumber ( 0 );
  IsNumber ( -0 );
  IsNumber ( 0.1 );
  IsNumber ( 1 );
  IsNumber ( 1.2 );
  IsNumber ( -1.2 );
  IsNumber ( 1.234e+124 );
  IsNumber ( -1.234e+124 );
  IsNumber ( 10^50 );
  "These should be false";
  IsNumber ( "abc" );
  IsNumber ( "1.2.3" );
  IsNumber ( "0001" );
)
*/
