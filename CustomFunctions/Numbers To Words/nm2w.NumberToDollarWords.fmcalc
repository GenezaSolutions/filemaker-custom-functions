// nm2w.NumberToDollarWords ( _amount )
//
// Purpose:		Converts a dillar amount into words for a check-writing system.
//
// Parameters:		_amount:	The amount to conver to a check-compatible version
//
// Requirements: 	nm2w.IntegerToWords
//
// Author:			The Shadow
// Version:		1.0 written 15-03-11
//
// Notes:			The text is all lower-case, some people might prefer the output in proper case, which is
//				easy to do: Proper ( nm2w.NumberToDollarWords ( _amount ) ).
//
//				It supports more money than you will ever have to write a check for.
//
// Example:
// nm2w.NumbertoDollarWords ( 97906317437.15 ) = "ninety-seven billion nine hundred six million " &
//   "three hundred seventeen thousand four hundred thirty-seven dollars and 15/100"

Let(
  [
    _rounded_amount		= Round( _amount; 2 );
    _dollars				= Int( _rounded_amount );
    _cents				= ( ( _rounded_amount - _dollars ) * 100 );

    _dollar_portion = Case(
      _dollars = 0;		"zero";
    					nm2w.IntegerToWords( _dollars; 10 ^ 63 )
    ) & " dollars and ";

    _cents_portion = Case(
      _cents = 0;		"no";
    				_cents
    ) & "/100";

    _result = _dollar_portion & _cents_portion
  ];

  _result
)
