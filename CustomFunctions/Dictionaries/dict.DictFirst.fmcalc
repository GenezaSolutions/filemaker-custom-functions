// Returns the first key in the dictionary.
//
// Written by Jesse Antunes
//   http://sixfriedrice.com/wp/filemaker-dictionary-functions/

Let(
  [
    _startValue    = "<:";
    _endValue      = ":=";
    _startValuePos = Position( _dict; _startValue; 1; 1 );
    
    _startValueEnd = Case(
      _startValuePos > 0;
      _startValuePos + Length( _startValue );
      -1
    );
    
    _endValuePos   = Position( _dict; _endValue; _startValueEnd; 1 );
    _found         = ( _endValuePos > -1 ) and ( _startValueEnd > -1 );
    _foundValueLen = _endValuePos - _startValueEnd;
    _foundValue    = Middle( _dict; _startValueEnd; _foundValueLen )
  ];

  Case(
    _found;
    _foundValue;
    devp.Nil
  )
)
