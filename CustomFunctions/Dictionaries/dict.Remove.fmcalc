// dict.Remove ( _dict; _key )
//
// Purpose:		Removes the name from the dictionary.
//
// Parameters:		_dict:	The dictionary to modify
//				_key:		The key to remove
//
// Requirements: 	dict.Escape
//
// Author:			Jesse Antunes
// Version:		1.0 written 15-03-11
//
// Notes:			http://sixfriedrice.com/wp/filemaker-dictionary-functions/
//
// Example:
// Let (
//   [
//     _dict = dict.Add ( devp.Nil; "key1" ; "value1" );
//     _dict = dict.Add ( _dict; "key2"; "value2" )
//   ];
// 
//   dict.Remove ( _dict; "key1" )
// ) = "<:key2:=value2:>"

Let(
  [
    _pattern		= "<:" & dict.Escape( _key ) & ":=";

    _entry_start		= Position( _dict; _pattern; 1; 1 );
    _entry_end		= Position( _dict; ":>"; _entry_start + 1; 1 );

    _dict_beginning	= Case(
      _entry_start > 0;		Left( _dict; _entry_start - 1 )
    );

    _rest			= Middle( _dict; _entry_end + 2; 999999 );
    _new_dict		= _dict_beginning & _rest;

    _result = Case (
      _entry_start > 0;		_new_dict;
					_dict
    )
  ];

  _result
)
